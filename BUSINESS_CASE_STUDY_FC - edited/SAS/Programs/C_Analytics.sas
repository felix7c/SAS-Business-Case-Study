*********************************************************************
*																  	*
*  Name: C_Analytics.sas											*
*  Description: Analysing case study data and creating reports		*
* 				from this for the Business Case Study.				*
*																	*
*  Creation Date: 2021-08-23										*
*																	*
*********************************************************************
;


*send logs to a text file rather than SAS log;
proc printto log="&sas_root.\Logs\C_Data_Log.txt";
run;



*Create proc means and transpose it;
%Means_Transposed(detail.households_detail,) *second arg left as blank as not classifying by any variable;

*setting correct interest names, as not all have labels;
data marts.interests_freq (label="Interests Frequency Counts (Generated by &sysuserid. on &today_f. at &systime.)"  drop=_label_ _name_);
  retain Interest; *so interest comes first;
  set work.interests_freq__t (rename=(col1=Count)) ;
  if missing(_label_)=0 then Interest= _label_;
  else Interest=_name_;
run;





*Age Band freq counts stuff (for those who have made a booking);
*create bookings_made dataset;
proc sql noprint;
  create table work.bookings_made as
  select h.*, b.departure_date
  from detail.households_detail h inner join staging.bookings b
    on h.customer_id=b.customer_id
  where h.customer_id in 
    (select customer_id from staging.bookings)  and pri_householder
  order by h.customer_id;
quit;


*apply age format and save bookings_made;
data work.bookings_made_age (label="Bookings Made Dataset (Generated by &sysuserid. on &today_f. at &systime.)" );
  set work.bookings_made;
  if missing(dob)=0 and missing(departure_date)=0 then Age= floor(yrdif(DOB,departure_date,'ACT/ACT')); *sets value for age variable (rounded down using floor, but ACT/ACT  for accurate age);
  format Age age_fmt.;
run;


*Create proc means and transpose it;
%Means_Transposed(work.bookings_made_age, age)


*setting correct interest names, as not all have labels;
data marts.interests_freq_age (label="Interests Frequency Counts By Age (Generated by &sysuserid. on &today_f. at &systime.)"
                               drop=_label_ _name_ rename=(col1=Missing col2=a25_34 col3=a35_44 col4=a45_54 col5=a55_64 col6=a65_plus) );
  retain Interest; *so interest comes first;
  set work.interests_freq_age_t ;
  if missing(_label_)=0 then Interest= _label_;
  else Interest=_name_;
  label col2='25-34' col3='35-44' col4='45-54'col5='55-64' col6='65+';
  if _n_=1 then delete; *deletes first row which is just age;
run;






*Getting interest frequency by gender and transposing;
%Means_Transposed(detail.households_detail, Gender)


*Setting correct interest names, as not all have labels;
data marts.interests_freq_gender (label="Interests Frequency Counts By Gender (Generated by &sysuserid. on &today_f. at &systime.)"  drop=_label_ _name_);
  retain Interest; *so interest comes first;
  set work.interests_freq_gender_t (rename=(col1=Count col2=Count_F col3=Count_M)) ;
  if missing(_label_)=0 then Interest= _label_;
  else Interest=_name_;
  label count='Count' count_f='Count' count_m='Count';
run;

*sort descending by gender (so can keep only top 5 obs for that col, drop other cols);
proc sort data=marts.interests_freq_gender out=work.interests_freq_gender_f;
  by descending count_f;
run;

proc sort data=marts.interests_freq_gender out=work.interests_freq_gender_m;
  by descending count_m;
run;




*Create and save PDF file;

ods pdf file="&sas_root.\Report\C_Report.pdf" style= Pearl; *location to save as pdf;

  title3 "Frequency Counts of Each Holiday Interest";
  ods results=off; *supress pdf from being opened;
  proc print data=marts.interests_freq label ; *runs proc print;
  run;
  
  title3 "Frequency Counts of Each Holiday Interest (By Age Group)";
  ods results=off; *supress pdf from being opened;
  proc print data=marts.interests_freq_age label ; *runs proc print;
  run;
  title3;

ods pdf close;





*Gender excel document;
ods excel file="&sas_root.\Report\C_Report_Gender.xlsx" style=Pearl; *location to save as pdf;
 
  ods excel options(sheet_name="Female Counts");
  proc print data=work.interests_freq_gender_f (obs=5 drop=count count_m);
  run;
 
  ods excel options(sheet_name="Male Counts");
  proc print data=work.interests_freq_gender_m (obs=5 drop=count count_f);
  run;
ods excel close;





*resets so logs sent to log from now on;
proc printto;
run;